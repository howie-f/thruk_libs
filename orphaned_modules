#!/usr/bin/perl

use warnings;
use strict;
use Getopt::Long;

use lib 'lib';
use BuildHelper;

BEGIN {
    for my $val (qw/PERL5LIB PERL_MB_OPT PERL_LOCAL_LIB_ROOT PERL_MM_OPT/) {
        undef $ENV{$val} if defined $ENV{$val};
    }
};

####################################
my $verbose = 0;
GetOptions ('v|verbose' => \$verbose);

####################################
my @packages     = glob("../thruk/Makefile.PL");
my $more_modules = {
        'File::Copy::Recursive'         => '0',         # required by Thruks inc::Module::Install
};

####################################
# get module dependencies
my($deps,$files) = BuildHelper::get_all_deps(1);
my $orphaned     = BuildHelper::get_orphaned($deps, $files, $verbose);

####################################
# remove referenced modules
for my $p (@packages) {
    my $meta     = BuildHelper::get_meta($p);
    my $rem_deps = BuildHelper::get_deps_from_meta($meta, 1);
    for my $rem_dep (keys %{$rem_deps}) {
        my $rdep = BuildHelper::module_to_file($rem_dep, $files, $rem_deps->{$rem_dep});
        my $cv   = BuildHelper::is_core_module($rem_dep, 5.008);
        if(!$rdep and !BuildHelper::version_compare($cv, $rem_deps->{$rem_dep}) and $rem_dep !~ m/^(Test|Devel|WWW::Mechanize::Firefox)/mx) {
            print "WARNING: ",$rem_dep, " does not resolve to a file\n";
        } else {
            delete $orphaned->{$rdep} if $rdep;
        }
    }
}

####################################
for my $f (keys %{$files}) {
    my($m,$v) = BuildHelper::file_to_module($files->{$f});
    my $cv    = BuildHelper::is_core_module($m, 5.008);
    if($cv and BuildHelper::version_compare($cv, $v)) {
        print "WARNING: ",$m, " is a core module (".$cv.") and ",$files->{$m}," should be removed\n";
    }
}

# check other modules
for my $m (keys %{$more_modules}) {
    if(BuildHelper::is_core_module($m, 5.008)) {
        print "WARNING: ",$m, " is a core module and should be removed\n";
        next;
    }
    my $mdep = BuildHelper::module_to_file($m, $files, $more_modules->{$m});
    if(!defined $mdep) {
        print "WARNING: ",$m, " does not resolve to a file\n";
    } else {
        delete $orphaned->{$mdep};
    }
}

####################################
# print result
for my $file (keys %{$orphaned}) {
    my($m,$v) = BuildHelper::file_to_module($file);
    if(!defined $more_modules->{$m}) {
        print $file, " is orphaned and could probably removed\n";
    }
}

